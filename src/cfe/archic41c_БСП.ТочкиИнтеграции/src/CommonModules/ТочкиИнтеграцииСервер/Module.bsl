
#Область ПрограммныйИнтерфейс

// Возвращает таблицу связей домена из регистра с отбором по организации (без исключенных связей)
//
// Параметры:
//	ДатаСреза - Дата
//  Организация	 - СправочникСсылка.Организации - организация
// 
// Возвращаемое значение:
//  ТаблицаЗначений - содержит колонки регистра сведений СвязиБизнесДоменов
//
Функция СвязиДоменовПоОрганизации(ДатаСреза, Организация) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СвязиБизнесДоменовСрезПоследних.Организация КАК Организация,
	               |	СвязиБизнесДоменовСрезПоследних.Источник КАК Источник,
	               |	СвязиБизнесДоменовСрезПоследних.Приемник КАК Приемник,
	               |	СвязиБизнесДоменовСрезПоследних.ОписаниеПотока КАК ОписаниеПотока,
	               |	СвязиБизнесДоменовСрезПоследних.ОписаниеИзменения КАК ОписаниеИзменения,
	               |	СвязиБизнесДоменовСрезПоследних.Приоритет КАК Приоритет,
	               |	СвязиБизнесДоменовСрезПоследних.Действия КАК Действия
	               |ИЗ
	               |	РегистрСведений.СвязиБизнесДоменов.СрезПоследних(&ДатаСреза, Организация = &Организация) КАК СвязиБизнесДоменовСрезПоследних
	               |ГДЕ
	               |	СвязиБизнесДоменовСрезПоследних.Действия <> ЗНАЧЕНИЕ(Перечисление.ДействияПоАрхитектуре.Исключается)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Приоритет";
	
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	ТаблицаРезультата = Результат.Выгрузить();
	
	Возврат ТаблицаРезультата;

КонецФункции

// Возвращает таблицу доменов организации
// Не входят отмененные и отложенные домены в список возвращаемых
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации - организация
//  ДокументАрхитектурноеРешение - ДокументСсылка.АрхитектурныеРешения - документ в рамках которого сделаны изменения по домену (необязательный параметр)
// 
// Возвращаемое значение:
//  ТаблицаЗначений - содержит колонки:
//		* БизнесДомен - СправочникСсылка.БизнесДомены
//		* Изменяется - Булево - Истина, в случае если было изменение в документе "Архитектурные решения"
//		* Описание - Строка - описания архитектурных изменений по документу
//
Функция ДоменыОрганизации(Организация, ДокументАрхитектурноеРешение = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	АрхитектурныеРешенияИзмененияДоменов.БизнесДомен КАК БизнесДомен,
	               |	АрхитектурныеРешенияИзмененияДоменов.ОписаниеИзменения КАК ОписаниеИзменения
	               |ПОМЕСТИТЬ ВТ_ИзменяемыеДоменыДокумента
	               |ИЗ
	               |	Документ.АрхитектурноеРешение.ИзмененияДоменов КАК АрхитектурныеРешенияИзмененияДоменов
	               |ГДЕ
	               |	АрхитектурныеРешенияИзмененияДоменов.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	БизнесДомены.Ссылка КАК БизнесДомен,
	               |	БизнесДомены.Владелец КАК Владелец,
	               |	ВЫБОР
	               |		КОГДА ВТ_ИзменяемыеДоменыДокумента.БизнесДомен ЕСТЬ NULL
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК Изменяется,
	               |	ЕСТЬNULL(ВТ_ИзменяемыеДоменыДокумента.ОписаниеИзменения, """") КАК ОписаниеИзменения
	               |ПОМЕСТИТЬ ВТ_ДоменыСУчетомАрхРешения
	               |ИЗ
	               |	Справочник.БизнесДомены КАК БизнесДомены
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИзменяемыеДоменыДокумента КАК ВТ_ИзменяемыеДоменыДокумента
	               |		ПО (ВТ_ИзменяемыеДоменыДокумента.БизнесДомен = БизнесДомены.Ссылка)
	               |ГДЕ
	               |	БизнесДомены.Владелец.Организация = &Организация
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ДоменыСУчетомАрхРешения.БизнесДомен КАК БизнесДомен,
	               |	ВТ_ДоменыСУчетомАрхРешения.Владелец КАК Владелец,
	               |	ВТ_ДоменыСУчетомАрхРешения.Изменяется КАК Изменяется,
	               |	ВТ_ДоменыСУчетомАрхРешения.ОписаниеИзменения КАК ОписаниеИзменения
	               |ИЗ
	               |	ВТ_ДоменыСУчетомАрхРешения КАК ВТ_ДоменыСУчетомАрхРешения
	               |ГДЕ
	               |	(ВТ_ДоменыСУчетомАрхРешения.Изменяется
	               |			ИЛИ НЕ ВТ_ДоменыСУчетомАрхРешения.Изменяется
	               |				И НЕ ВТ_ДоменыСУчетомАрхРешения.БизнесДомен.СтатусЗапуска В (ЗНАЧЕНИЕ(Перечисление.СтатусБизнесДомена.Отложено), ЗНАЧЕНИЕ(Перечисление.СтатусБизнесДомена.Отменено)))";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Ссылка", ДокументАрхитектурноеРешение);
	
	Результат = Запрос.Выполнить();
	ТаблицаРезультата = Результат.Выгрузить();
	
	Возврат ТаблицаРезультата;

КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти