// BSLLS:LatinAndCyrillicSymbolInWord-off
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ИсходныйКод) Тогда
	    ИсходныйКод = Параметры.ИсходныйКод;
	Иначе	
		ЗаполнитьИсходникПоУмолчанию();
	КонецЕсли;
	
	ЗаполнитьНастройкиПоУмолчанию();
	
	Если Параметры.СразуСформировать Тогда
		СгенерироватьНаСервере();	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Сгенерировать(Команда)
	СгенерироватьНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьИсходникПоУмолчанию()
	ИсходныйКод = НСтр("ru = ''' Пример работы по-умолчанию
                        |
                        |Alice -> Bob: Authentication Request
                        |Bob --> Alice: Authentication Response
                        |		
                        |Alice -> Bob: Another authentication Request
                        |Alice <-- Bob: another authentication Response'");
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиПоУмолчанию()
	
	ГенерацияUMLДиаграмм = РеквизитФормыВЗначение("Объект");
	
	ПараметрыПоУмолчанию = ГенерацияUMLДиаграмм.ПараметрыПоУмолчанию();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыПоУмолчанию);
	
КонецПроцедуры

&НаСервере
Процедура СгенерироватьНаСервере()
	
	ГенерацияUMLДиаграмм = РеквизитФормыВЗначение("Объект");
	
	ПараметрыПоУмолчанию = ГенерацияUMLДиаграмм.ПараметрыПоУмолчанию();
	ЗаполнитьЗначенияСвойств(ПараметрыПоУмолчанию, ЭтотОбъект);
	Результат = ГенерацияUMLДиаграмм.СгенерированноеИзображение(ИсходныйКод, ПараметрыПоУмолчанию);
	Если ЗначениеЗаполнено(Результат.АдресИзображения) Тогда
	 	РезультатОбработки = Результат.АдресИзображения;
		СсылкаНаPlantUML = Результат.СсылкаБазовая;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти
