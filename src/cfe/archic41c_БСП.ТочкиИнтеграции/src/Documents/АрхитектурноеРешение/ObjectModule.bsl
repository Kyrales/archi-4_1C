
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.СвязиБизнесДоменов.Записывать = Истина;
		
	Для Каждого ТекСтрока Из СвязиДоменов Цикл
		
		Если Не ЗначениеЗаполнено(ТекСтрока.Действия) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекСтрока.Действия = Перечисления.СтатусАрхитектурногоРешения.Проект
			Или ТекСтрока.Действия = Перечисления.СтатусАрхитектурногоРешения.Отклонен Тогда
			Продолжить;
		КонецЕсли;
		
		Движение = Движения.СвязиБизнесДоменов.Добавить();
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.Источник = ТекСтрока.Источник;
		Движение.Приемник = ТекСтрока.Приемник;
		
		Движение.Действия = ТекСтрока.Действия;
		Движение.ОписаниеПотока = ТекСтрока.ОписаниеПотока;
		Движение.ОписаниеИзменения = ТекСтрока.ОписаниеИзменения;
		Движение.Приоритет = ?(ТекСтрока.Приоритет = 0, 99, ТекСтрока.Приоритет);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли

